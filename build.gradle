plugins {
    id "org.openapi.generator" version "7.0.1"
}

def spec = "$rootDir/doc/swagger.yaml"
def group = "com.xingzhi"
def ver = "1.0.5"

// https://github.com/OpenAPITools/openapi-generator/tree/master/modules/openapi-generator-gradle-plugin
// https://openapi-generator.tech/docs/generators
openApiValidate {
    inputSpec = spec
}

openApiGenerate {
    generatorName = "kotlin"
    inputSpec = spec
    outputDir = "$rootDir/api"
//    remoteInputSpec = "https://openapi.shxzzn.com/swagger/v1/swagger.json"
    groupId = group
    version = ver
    packageName = "com.xingzhi.robot"
    apiPackage = "com.xingzhi.robot.api"
    modelPackage = "com.xingzhi.robot.model"
//    modelNameSuffix = "Dto"
//    invokerPackage = "com.xingzhi.robot.invoker"
    configOptions = [
            library             : "jvm-retrofit2",
            useCoroutines       : "true",
            serializationLibrary: "kotlinx_serialization",
            dateLibrary         : "threetenbp-localdatetime",
            omitGradleWrapper   : "true",
            useSettingsGradle   : "true",
    ]
}

project("api") {
    apply plugin: 'java-library'
    apply plugin: "maven-publish"

    java {
        withJavadocJar()
        withSourcesJar()
    }

    jar {
        metaInf {
            from layout.projectDirectory.file('proguard-rules.pro')
        }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/OpenXingZhi/OpenAPI")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }

        publications {
            binary(MavenPublication) {
                groupId = group
                artifactId = project.name
                version = ver

                from components.java
            }
        }
    }
}